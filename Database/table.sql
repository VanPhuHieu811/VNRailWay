-- =============================================
-- 1. KHỞI TẠO DATABASE
-- =============================================
USE master
GO
IF DB_ID(N'VNRAILWAY') IS NOT NULL
BEGIN
    ALTER DATABASE VNRAILWAY SET SINGLE_USER WITH ROLLBACK IMMEDIATE;
    DROP DATABASE VNRAILWAY;
END;
GO
CREATE DATABASE VNRAILWAY;
GO
USE VNRAILWAY;
GO

-- =============================================
-- 2. TẠO BẢNG (CHỈ BAO GỒM PK & CHECK CONSTRAINT)
-- =============================================

-- 1. Bảng THAM_SO
CREATE TABLE THAM_SO (
    MaThamSo VARCHAR(10) NOT NULL,
    TenThamSo NVARCHAR(100),
    GiaTriSo FLOAT,
    GiaTriChuoi NVARCHAR(255),
    NgayHieuLuc DATETIME,
    NgayHetHieuLuc DATETIME,
    MoTa NVARCHAR(255),
    CONSTRAINT PK_THAM_SO PRIMARY KEY (MaThamSo)
);

-- 2. Bảng GA_TAU
CREATE TABLE GA_TAU (
    MaGaTau VARCHAR(10) NOT NULL,
    TenGa NVARCHAR(100) NOT NULL,
    DiaChi NVARCHAR(255) NOT NULL,
    CONSTRAINT PK_GA_TAU PRIMARY KEY (MaGaTau)
);

-- 3. Bảng UU_DAI_GIA
CREATE TABLE UU_DAI_GIA (
    MaUuDai VARCHAR(10) NOT NULL,
    LoaiUuDai NVARCHAR(50),
    MoTa NVARCHAR(255),
    DoiTuong NVARCHAR(50),
    PhanTram INT,
    CONSTRAINT PK_UU_DAI_GIA PRIMARY KEY (MaUuDai),
    CONSTRAINT CK_PhanTramUuDai CHECK (PhanTram >= 0 AND PhanTram <= 100)
);

-- 4. Bảng DOAN_TAU
CREATE TABLE DOAN_TAU (
    MaDoanTau VARCHAR(10) NOT NULL,
    TenTau NVARCHAR(100) NOT NULL,
    HangSanXuat NVARCHAR(100),
    NgayVanHanh DATE NOT NULL,
    LoaiTau NVARCHAR(20) NOT NULL,
    CONSTRAINT PK_DOAN_TAU PRIMARY KEY (MaDoanTau),
    CONSTRAINT CK_LoaiTau CHECK (LoaiTau IN (N'Hạng sang', N'Bình thường'))
);

-- 5. Bảng TOA_TAU
CREATE TABLE TOA_TAU (
    MaToaTau VARCHAR(10) NOT NULL,
    MaDoanTau VARCHAR(10) NOT NULL,
    STT INT NOT NULL,
    LoaiToa NVARCHAR(10) NOT NULL,
    SLViTri INT,
    CONSTRAINT PK_TOA_TAU PRIMARY KEY (MaToaTau),
    CONSTRAINT CK_LoaiToa CHECK (LoaiToa IN (N'Ghế', N'Giường'))
);

-- 6. Bảng VI_TRI_TREN_TOA
CREATE TABLE VI_TRI_TREN_TOA (
    MaViTri VARCHAR(10) NOT NULL,
    MaToaTau VARCHAR(10) NOT NULL,
    STT INT,
    LoaiVT NVARCHAR(10) NOT NULL,
    CONSTRAINT PK_VI_TRI_TREN_TOA PRIMARY KEY (MaViTri),
    CONSTRAINT CK_LoaiVT CHECK (LoaiVT IN (N'Ghế', N'Giường'))
);

-- 7. Bảng GIUONG
CREATE TABLE GIUONG (
    MaViTri VARCHAR(10) NOT NULL,
    Tang INT,
    Phong INT,
    CONSTRAINT PK_GIUONG PRIMARY KEY (MaViTri)
);

-- 8. Bảng TUYEN_TAU
CREATE TABLE TUYEN_TAU (
    MaTuyenTau VARCHAR(10) NOT NULL,
    TenTuyen NVARCHAR(50),
    KhoangCach FLOAT,
    CONSTRAINT PK_TUYEN_TAU PRIMARY KEY (MaTuyenTau)
);

-- 9. Bảng DANH_SACH_GA
CREATE TABLE DANH_SACH_GA (
    MaTuyenTau VARCHAR(10) NOT NULL,
    MaGaTau VARCHAR(10) NOT NULL,
    ThuTu INT,
    KhoangCach FLOAT,
    CONSTRAINT PK_DANH_SACH_GA PRIMARY KEY (MaTuyenTau, MaGaTau)
);

-- 10. Bảng NHAN_VIEN
CREATE TABLE NHAN_VIEN (
    MaNV VARCHAR(10) NOT NULL,
    HoTen NVARCHAR(100) NOT NULL,
    CCCD VARCHAR(12) NOT NULL,
    NgaySinh DATE,
    GioiTinh NVARCHAR(10),
    DiaChi NVARCHAR(255),
    SoDienThoai VARCHAR(10),
    LoaiNhanVien NVARCHAR(20) NOT NULL,
    NVQuanLy VARCHAR(10),
    CONSTRAINT PK_NHAN_VIEN PRIMARY KEY (MaNV),
    CONSTRAINT UQ_NHANVIEN_CCCD UNIQUE (CCCD),
    CONSTRAINT CK_GioiTinhNV CHECK (GioiTinh IN (N'Nam', N'Nữ')),
    CONSTRAINT CK_LoaiNhanVien CHECK (LoaiNhanVien IN (N'Quản lý', N'Bán vé', N'Lái tàu', N'Toa tàu', N'Quản trị')),
    CONSTRAINT CK_TuQuanLy CHECK (MaNV <> NVQuanLy)
);

-- 11. Bảng KHACH_HANG
CREATE TABLE KHACH_HANG (
    MaKhachHang VARCHAR(10) NOT NULL,
    HoTen NVARCHAR(50) NOT NULL,
    CCCD VARCHAR(12) NOT NULL,
    NgaySinh DATE NOT NULL,
    GioiTinh NVARCHAR(10) NOT NULL,
    DiaChi NVARCHAR(255) NOT NULL,
    SoDienThoai VARCHAR(10) NOT NULL,
    CONSTRAINT PK_KHACH_HANG PRIMARY KEY (MaKhachHang),
    CONSTRAINT UQ_KHACHHANG_CCCD UNIQUE (CCCD),
    CONSTRAINT CK_GioiTinhKH CHECK (GioiTinh IN (N'Nam', N'Nữ'))
);

-- 12. Bảng TAI_KHOAN
CREATE TABLE TAI_KHOAN (
    Email VARCHAR(255) NOT NULL,
    MaKH VARCHAR(10),
    MaNV VARCHAR(10),
    TenTaiKhoan VARCHAR(50), 
    MatKhau VARCHAR(50) NOT NULL,
    TrangThai BIT NOT NULL,
    VaiTro NVARCHAR(20),
    CONSTRAINT PK_TAI_KHOAN PRIMARY KEY (Email),
    CONSTRAINT UQ_TenTaiKhoan UNIQUE (TenTaiKhoan),
    CONSTRAINT CK_VaiTro CHECK (VaiTro IN (N'Khách hàng', N'Nhân viên', N'Quản trị'))
);

-- 13. Bảng DON_NGHI_PHEP
CREATE TABLE DON_NGHI_PHEP (
    MaDon VARCHAR(10) NOT NULL,
    MaPhanCong VARCHAR(10) NOT NULL, -- Cột này gây lỗi ở bản cũ
    NgayGui DATETIME NOT NULL,
    LyDo NVARCHAR(255),
    NVGuiDon VARCHAR(10) NOT NULL,
    NVDuyetDon VARCHAR(10),
    NVThayThe VARCHAR(10),
    TrangThai NVARCHAR(20) NOT NULL,
    CONSTRAINT PK_DON_NGHI_PHEP PRIMARY KEY (MaDon),
    CONSTRAINT CK_TrangThaiDon CHECK (TrangThai IN (N'Đang chờ', N'Chấp nhận', N'Từ chối'))
);

-- 14. Bảng PHAN_CONG_BAN_VE
CREATE TABLE PHAN_CONG_BAN_VE (
    MaNV VARCHAR(10) NOT NULL,
    MaGa VARCHAR(10) NOT NULL,
    NgayBatDau DATETIME NOT NULL,
    NgayKetThuc DATETIME,
    CONSTRAINT PK_PHAN_CONG_BAN_VE PRIMARY KEY (MaNV, MaGa, NgayBatDau),
    CONSTRAINT CK_NgayLamViec CHECK (NgayKetThuc IS NULL OR NgayBatDau < NgayKetThuc)
);

-- 15. Bảng CHUYEN_TAU
CREATE TABLE CHUYEN_TAU (
    MaChuyenTau VARCHAR(10) NOT NULL,
    MaTuyenTau VARCHAR(10),
    MaDoanTau VARCHAR(10),
    GaXuatPhat VARCHAR(10),
    GaKetThuc VARCHAR(10),
    TrangThai NVARCHAR(20) NOT NULL,
    CONSTRAINT PK_CHUYEN_TAU PRIMARY KEY (MaChuyenTau),
    CONSTRAINT CK_TrangThaiChuyenTau CHECK (TrangThai IN (N'Chuẩn bị', N'Đang chạy', N'Hoàn thành', N'Hủy'))
);

-- 16. Bảng THOI_GIAN_CHUYEN_TAU
CREATE TABLE THOI_GIAN_CHUYEN_TAU (
    MaChuyenTau VARCHAR(10) NOT NULL,
    MaGaTau VARCHAR(10) NOT NULL,
    DuKienXuatPhat DATETIME NOT NULL,
    DuKienDen DATETIME NOT NULL,
    ThucTeXuatPhat DATETIME,
    ThucTeDen DATETIME,
    CONSTRAINT PK_THOI_GIAN_CHUYEN_TAU PRIMARY KEY (MaChuyenTau, MaGaTau)
);

-- 17. Bảng PHAN_CONG_CHUYEN_TAU
CREATE TABLE PHAN_CONG_CHUYEN_TAU (
    MaPhanCong VARCHAR(10) NOT NULL,
    MaNV VARCHAR(10) NOT NULL,
    VaiTro NVARCHAR(30) NOT NULL,
    MaChuyenTau VARCHAR(10) NOT NULL,
    MaToa VARCHAR(10),
    TrangThai NVARCHAR(20) NOT NULL,
    CONSTRAINT PK_PHAN_CONG_CHUYEN_TAU PRIMARY KEY (MaPhanCong),
    CONSTRAINT CK_VaiTroPC CHECK (VaiTro IN (N'Nhân viên phụ trách lái', N'Nhân viên trưởng', N'Nhân viên phụ trách toa')),
    CONSTRAINT CK_TrangThaiPC CHECK (TrangThai IN (N'Nhận việc', N'Nghỉ', N'Đang làm', N'Xong'))
);

-- 18. Bảng BANG_LUONG
CREATE TABLE BANG_LUONG (
    MaBangLuong VARCHAR(10) NOT NULL,
    NgayNhanLuong DATETIME NOT NULL,
    MaNV VARCHAR(10) NOT NULL,
    LuongChinh DECIMAL(18, 0) NOT NULL,
    PhuCap DECIMAL(18, 0),
    TienPhat DECIMAL(18, 0),
    ThuLaoChuyenTau DECIMAL(18, 0),
    ThuLaoThayCa DECIMAL(18, 0),
    TongLuong DECIMAL(18, 0),
    CONSTRAINT PK_BANG_LUONG PRIMARY KEY (MaBangLuong)
);

-- 19. Bảng CHINH_SACH_GIA
CREATE TABLE CHINH_SACH_GIA (
    MaBangGia VARCHAR(10) NOT NULL,
    NgayApDung DATETIME,
    NgayKetThuc DATETIME,
    LoaiTau NVARCHAR(20),
    LoaiVT NVARCHAR(10),
    Tang INT,
    MaDoanTau VARCHAR(10) NOT NULL,
    SoKm FLOAT,
    GiaTien DECIMAL(18, 0),
    CONSTRAINT PK_CHINH_SACH_GIA PRIMARY KEY (MaBangGia),
    CONSTRAINT CK_ThoiHanGia CHECK (NgayApDung < NgayKetThuc)
);

-- 20. Bảng DAT_VE
CREATE TABLE DAT_VE (
    MaDatVe VARCHAR(10) NOT NULL,
    Email VARCHAR(255) NOT NULL,
    MaChuyenTau VARCHAR(10) NOT NULL,
    ThoiGianDat DATETIME NOT NULL,
    HanThanhToan DATETIME NOT NULL,
    TongTienDuKien DECIMAL(18, 0),
    KenhDat NVARCHAR(20),
    TrangThai NVARCHAR(20),
    CONSTRAINT PK_DAT_VE PRIMARY KEY (MaDatVe),
    CONSTRAINT CK_KenhDat CHECK (KenhDat IN (N'Online', N'Tại quầy')),
    CONSTRAINT CK_TrangThaiDatVe CHECK (TrangThai IN (N'Đang chờ', N'Đã thanh toán', N'Hủy'))
);

-- 21. Bảng VE_TAU
CREATE TABLE VE_TAU (
    MaVe VARCHAR(10) NOT NULL,
    MaKhachHang VARCHAR(10) NOT NULL,
    MaChuyenTau VARCHAR(10) NOT NULL,
    MaDatVe VARCHAR(10) NOT NULL,
    MaBangGia VARCHAR(10) NOT NULL,
    MaUuDai VARCHAR(10),
    ThoiGianXuatVe DATETIME,
    GaXuatPhat VARCHAR(10),
    GaDen VARCHAR(10),
    SoTienGiam DECIMAL(18, 0),
    GiaThuc DECIMAL(18, 0),
    TrangThai NVARCHAR(20) NOT NULL,
    MaViTri VARCHAR(10) NOT NULL,
    CONSTRAINT PK_VE_TAU PRIMARY KEY (MaVe),
    CONSTRAINT CK_TrangThaiVe CHECK (TrangThai IN (N'Giữ chỗ', N'Đã đặt', N'Đã dùng', N'Hủy vé', N'Đổi vé'))
);

-- 22. Bảng DOI_VE
CREATE TABLE DOI_VE (
    MaDoiVe VARCHAR(10) NOT NULL,
    NVThucHien VARCHAR(10),
    MaVeCu VARCHAR(10) NOT NULL,
    MaVeMoi VARCHAR(10) NOT NULL,
    ThoiGianDoi DATETIME NOT NULL,
    PhiPhat DECIMAL(18, 0),
    TrangThai VARCHAR(20) NOT NULL,
    CONSTRAINT PK_DOI_VE PRIMARY KEY (MaDoiVe),
    CONSTRAINT CK_TrangThaiDoiVe CHECK (TrangThai IN ('Đang chờ', 'Đã đổi', 'Hủy'))
);

-- 23. Bảng HOA_DON
CREATE TABLE HOA_DON (
    MaHoaDon VARCHAR(10) NOT NULL,
    MaDatVe VARCHAR(10),
    MaDoiVe VARCHAR(10),
    MaNVLap VARCHAR(10),
    ThoiGianThanhToan DATETIME NOT NULL,
    HinhThucThanhToan NVARCHAR(20) NOT NULL,
    GiaTien DECIMAL(18, 0) NOT NULL,
    CONSTRAINT PK_HOA_DON PRIMARY KEY (MaHoaDon),
    CONSTRAINT CK_HinhThucTT CHECK (HinhThucThanhToan IN (N'Online', N'Tại quầy'))
);
GO

-- =============================================
-- 3. TẠO KHÓA NGOẠI (FOREIGN KEYS) - CHẠY SAU CÙNG
-- =============================================

-- TOA_TAU
ALTER TABLE TOA_TAU ADD CONSTRAINT FK_TOA_TAU_DOAN_TAU FOREIGN KEY (MaDoanTau) REFERENCES DOAN_TAU(MaDoanTau);

-- VI_TRI_TREN_TOA
ALTER TABLE VI_TRI_TREN_TOA ADD CONSTRAINT FK_VITRI_TOATAU FOREIGN KEY (MaToaTau) REFERENCES TOA_TAU(MaToaTau);

-- GIUONG
ALTER TABLE GIUONG ADD CONSTRAINT FK_GIUONG_VITRI FOREIGN KEY (MaViTri) REFERENCES VI_TRI_TREN_TOA(MaViTri);

-- DANH_SACH_GA
ALTER TABLE DANH_SACH_GA ADD CONSTRAINT FK_DSGA_TUYENTAU FOREIGN KEY (MaTuyenTau) REFERENCES TUYEN_TAU(MaTuyenTau);
ALTER TABLE DANH_SACH_GA ADD CONSTRAINT FK_DSGA_GATAU FOREIGN KEY (MaGaTau) REFERENCES GA_TAU(MaGaTau);

-- NHAN_VIEN (Đệ quy)
ALTER TABLE NHAN_VIEN ADD CONSTRAINT FK_NHANVIEN_QUANLY FOREIGN KEY (NVQuanLy) REFERENCES NHAN_VIEN(MaNV);

-- TAI_KHOAN
ALTER TABLE TAI_KHOAN ADD CONSTRAINT FK_TAIKHOAN_KHACHHANG FOREIGN KEY (MaKH) REFERENCES KHACH_HANG(MaKhachHang);
ALTER TABLE TAI_KHOAN ADD CONSTRAINT FK_TAIKHOAN_NHANVIEN FOREIGN KEY (MaNV) REFERENCES NHAN_VIEN(MaNV);

-- PHAN_CONG_BAN_VE
ALTER TABLE PHAN_CONG_BAN_VE ADD CONSTRAINT FK_PCBV_NHANVIEN FOREIGN KEY (MaNV) REFERENCES NHAN_VIEN(MaNV);
ALTER TABLE PHAN_CONG_BAN_VE ADD CONSTRAINT FK_PCBV_GATAU FOREIGN KEY (MaGa) REFERENCES GA_TAU(MaGaTau);

-- CHUYEN_TAU
ALTER TABLE CHUYEN_TAU ADD CONSTRAINT FK_CHUYENTAU_TUYENTAU FOREIGN KEY (MaTuyenTau) REFERENCES TUYEN_TAU(MaTuyenTau);
ALTER TABLE CHUYEN_TAU ADD CONSTRAINT FK_CHUYENTAU_DOANTAU FOREIGN KEY (MaDoanTau) REFERENCES DOAN_TAU(MaDoanTau);
ALTER TABLE CHUYEN_TAU ADD CONSTRAINT FK_CHUYENTAU_GAXP FOREIGN KEY (GaXuatPhat) REFERENCES GA_TAU(MaGaTau);
ALTER TABLE CHUYEN_TAU ADD CONSTRAINT FK_CHUYENTAU_GAKT FOREIGN KEY (GaKetThuc) REFERENCES GA_TAU(MaGaTau);

-- PHAN_CONG_CHUYEN_TAU 
ALTER TABLE PHAN_CONG_CHUYEN_TAU ADD CONSTRAINT FK_PCCT_NHANVIEN FOREIGN KEY (MaNV) REFERENCES NHAN_VIEN(MaNV);
ALTER TABLE PHAN_CONG_CHUYEN_TAU ADD CONSTRAINT FK_PCCT_CHUYENTAU FOREIGN KEY (MaChuyenTau) REFERENCES CHUYEN_TAU(MaChuyenTau);
ALTER TABLE PHAN_CONG_CHUYEN_TAU ADD CONSTRAINT FK_PCCT_TOATAU FOREIGN KEY (MaToa) REFERENCES TOA_TAU(MaToaTau);

-- DON_NGHI_PHEP 
ALTER TABLE DON_NGHI_PHEP ADD CONSTRAINT FK_DNP_NVGUI FOREIGN KEY (NVGuiDon) REFERENCES NHAN_VIEN(MaNV);
ALTER TABLE DON_NGHI_PHEP ADD CONSTRAINT FK_DNP_NVDUYETDON FOREIGN KEY (NVDuyetDon) REFERENCES NHAN_VIEN(MaNV);
ALTER TABLE DON_NGHI_PHEP ADD CONSTRAINT FK_DNP_NVTHAYTHE FOREIGN KEY (NVThayThe) REFERENCES NHAN_VIEN(MaNV);
ALTER TABLE DON_NGHI_PHEP ADD CONSTRAINT FK_DNP_MaPC FOREIGN KEY (MaPhanCong) REFERENCES PHAN_CONG_CHUYEN_TAU(MaPhanCong);

-- THOI_GIAN_CHUYEN_TAU
ALTER TABLE THOI_GIAN_CHUYEN_TAU ADD CONSTRAINT FK_TGCT_CHUYENTAU FOREIGN KEY (MaChuyenTau) REFERENCES CHUYEN_TAU(MaChuyenTau);
ALTER TABLE THOI_GIAN_CHUYEN_TAU ADD CONSTRAINT FK_TGCT_GATAU FOREIGN KEY (MaGaTau) REFERENCES GA_TAU(MaGaTau);

-- BANG_LUONG
ALTER TABLE BANG_LUONG ADD CONSTRAINT FK_BANGLUONG_NHANVIEN FOREIGN KEY (MaNV) REFERENCES NHAN_VIEN(MaNV);

-- CHINH_SACH_GIA
ALTER TABLE CHINH_SACH_GIA ADD CONSTRAINT FK_CSGIA_DOANTAU FOREIGN KEY (MaDoanTau) REFERENCES DOAN_TAU(MaDoanTau);

-- DAT_VE
ALTER TABLE DAT_VE ADD CONSTRAINT FK_DATVE_TAIKHOAN FOREIGN KEY (Email) REFERENCES TAI_KHOAN(Email);
ALTER TABLE DAT_VE ADD CONSTRAINT FK_DATVE_CHUYENTAU FOREIGN KEY (MaChuyenTau) REFERENCES CHUYEN_TAU(MaChuyenTau);

-- VE_TAU
ALTER TABLE VE_TAU ADD CONSTRAINT FK_VETAU_KHACHHANG FOREIGN KEY (MaKhachHang) REFERENCES KHACH_HANG(MaKhachHang);
ALTER TABLE VE_TAU ADD CONSTRAINT FK_VETAU_CHUYENTAU FOREIGN KEY (MaChuyenTau) REFERENCES CHUYEN_TAU(MaChuyenTau);
ALTER TABLE VE_TAU ADD CONSTRAINT FK_VETAU_DATVE FOREIGN KEY (MaDatVe) REFERENCES DAT_VE(MaDatVe);
ALTER TABLE VE_TAU ADD CONSTRAINT FK_VETAU_BANGGIA FOREIGN KEY (MaBangGia) REFERENCES CHINH_SACH_GIA(MaBangGia);
ALTER TABLE VE_TAU ADD CONSTRAINT FK_VETAU_UUDAI FOREIGN KEY (MaUuDai) REFERENCES UU_DAI_GIA(MaUuDai);
ALTER TABLE VE_TAU ADD CONSTRAINT FK_VETAU_GAXP FOREIGN KEY (GaXuatPhat) REFERENCES GA_TAU(MaGaTau);
ALTER TABLE VE_TAU ADD CONSTRAINT FK_VETAU_GADEN FOREIGN KEY (GaDen) REFERENCES GA_TAU(MaGaTau);
ALTER TABLE VE_TAU ADD CONSTRAINT FK_VETAU_VITRI FOREIGN KEY (MaViTri) REFERENCES VI_TRI_TREN_TOA(MaViTri);

-- DOI_VE
ALTER TABLE DOI_VE ADD CONSTRAINT FK_DOIVE_NHANVIEN FOREIGN KEY (NVThucHien) REFERENCES NHAN_VIEN(MaNV);
ALTER TABLE DOI_VE ADD CONSTRAINT FK_DOIVE_VECU FOREIGN KEY (MaVeCu) REFERENCES VE_TAU(MaVe);
ALTER TABLE DOI_VE ADD CONSTRAINT FK_DOIVE_VEMOI FOREIGN KEY (MaVeMoi) REFERENCES VE_TAU(MaVe);

-- HOA_DON
ALTER TABLE HOA_DON ADD CONSTRAINT FK_HOADON_DATVE FOREIGN KEY (MaDatVe) REFERENCES DAT_VE(MaDatVe);
ALTER TABLE HOA_DON ADD CONSTRAINT FK_HOADON_DOIVE FOREIGN KEY (MaDoiVe) REFERENCES DOI_VE(MaDoiVe);
ALTER TABLE HOA_DON ADD CONSTRAINT FK_HOADON_NHANVIEN FOREIGN KEY (MaNVLap) REFERENCES NHAN_VIEN(MaNV);
GO


-- Tạo dữ liệu giả để test
INSERT INTO KHACH_HANG (MaKhachHang, HoTen, CCCD, NgaySinh, GioiTinh, DiaChi, SoDienThoai)
VALUES ('KH_A', N'Nguyễn Văn A', '001', '1990-01-01', N'Nam', N'HN', '0901');

INSERT INTO KHACH_HANG (MaKhachHang, HoTen, CCCD, NgaySinh, GioiTinh, DiaChi, SoDienThoai)
VALUES ('KH_B', N'Trần Thị B', '002', '1992-02-02', N'Nữ', N'HCM', '0902');

INSERT INTO TAI_KHOAN (Email, MaKH, MatKhau, TrangThai, VaiTro, TenTaiKhoan) 
VALUES ('a@mail.com', 'KH_A', '123', 1, N'Khách hàng', 'user_a'); -- Điền user_a

INSERT INTO TAI_KHOAN (Email, MaKH, MatKhau, TrangThai, VaiTro, TenTaiKhoan) 
VALUES ('b@mail.com', 'KH_B', '123', 1, N'Khách hàng', 'user_b'); -- Điền user_b

-- Tạo chuyến tàu, đoàn tàu, toa tàu, vị trí
INSERT INTO GA_TAU VALUES ('GA_HN', N'Hà Nội', N'Hà Nội'), ('GA_SG', N'Sài Gòn', N'HCM');
INSERT INTO TUYEN_TAU VALUES ('TN1', N'Bắc Nam', 1700);
INSERT INTO DOAN_TAU VALUES ('DT01', 'SE1', 'VN', '2025-01-01', N'Hạng sang');
INSERT INTO TOA_TAU VALUES ('TOA01', 'DT01', 1, N'Ghế', 50);
INSERT INTO VI_TRI_TREN_TOA VALUES ('VT001', 'TOA01', 1, N'Ghế'); -- Vị trí cần tranh chấp

INSERT INTO CHUYEN_TAU VALUES ('CT01', 'TN1', 'DT01', 'GA_HN', 'GA_SG', N'Chuẩn bị');

-- Tạo bảng giá và đơn đặt vé (bắt buộc vì FK)
INSERT INTO CHINH_SACH_GIA (MaBangGia, MaDoanTau, GiaTien) VALUES ('BG01', 'DT01', 500000);
INSERT INTO DAT_VE (MaDatVe, Email, MaChuyenTau, ThoiGianDat, HanThanhToan) VALUES ('DV01', 'a@mail.com', 'CT01', GETDATE(), GETDATE()+1);